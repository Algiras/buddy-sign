= buddy-sign - High level message signing.
Andrey Antukh, <niwi@niwi.be>
:toc: left
:numbered:
:source-highlighter: pygments
:pygments-style: friendly
:sectlinks:

== Introduction

Buddy *sign* module is dedicated to provide a high level abstraction
for web ready message signing and encryption.

It can be used for several purposes:

* You can serialize and sign or encrypt a user ID for unsubscribing of newsletters into URLs.
  This way you don't need to generate one-time tokens and store them in the database.
* Same thing with any kind of activation link for accounts and similar things.
* Signed or encrypted objects can be stored in cookies or other untrusted sources which
  means you don't need to have sessions stored on the server, which reduces the number of necessary
  database queries.
* Signed information can safely do a roundtrip between server and client in general which makes
  them useful for passing server-side state to a client and then back.
* Safely send and receve signed or encrypted messages between components or microservices.
* Self contained token generation for use with completely stateless token based authentication.


== Project Maturity

Since _buddy-sign_ is a young project there can be some API breakage.


== Install

This section covers the _buddy-sign_ library installing process and its requirements.

=== Requirements

_buddy-sign_ is tested with these platforms:

- JDK7
- JDK8


=== Leiningen

The simplest way to use _buddy-sign_ in a clojure project, is by including it in the dependency
vector on your *_project.clj_* file:

._on project.clj_
[source,clojure]
----
[buddy/buddy-sign "0.5.0-SNAPSHOT"]
----


=== Gradle

If you are using gradle, this is a dependency line for gradle dsl:

[source,groovy]
----
compile "buddy:buddy-sign:0.5.0-SNAPSHOT"
----


=== Get the Code

_buddy-sign_ is open source and can be found on link:https://github.com/funcool/buddy-sign[github].

You can clone the public repository with this command:

[source,text]
----
git clone https://github.com/funcool/buddy-sign
----

== Json Web Token

JSON Web Token (JWT) is a compact claims representation format intended for space constrained
environments such as HTTP Authorization headers and URI query parameters.  JWTs encode claims
to be transmitted as a JavaScript Object Notation (JSON) object that is used as the payload of
a JSON Web Signature (JWS) structure or as the plaintext of a JSON Web Encryption (JWE) structure,
enabling the claims to be digitally signed or MACed and/or encrypted.


=== How to use it?

_buddy-sign_ does not offers special api for Json Web Token because it is a subset of Json Web
Signature (JWS) and Json Web Encryption (JSE) specifications. If you want use one or other see the
corresponding documentation in below sections.


=== Involved RFC's

* http://tools.ietf.org/html/draft-ietf-oauth-json-web-token-32
* http://tools.ietf.org/html/draft-ietf-jose-json-web-algorithms-40
* http://tools.ietf.org/html/draft-ietf-jose-json-web-encryption-40
* http://tools.ietf.org/html/draft-ietf-jose-json-web-signature-40
* http://tools.ietf.org/html/draft-mcgrew-aead-aes-cbc-hmac-sha2-05
* http://tools.ietf.org/html/rfc3394


[[jws]]
== Json Web Signature

[small]#New in version: 0.2#

JSON Web Signature (JWS) is a signing part of Json Web Token (JWT) specification and represents a
content secured with digital signatures or Message Authentication Codes (MACs) using JavaScript
Object Notation (JSON) as serialization format.


=== Supported algorithms

Here a summary of supported algorithms for JWS:

[options="header"]
|=====================================================================================
|Algorithm name     | Hash algorithms   | Keywords           | Priv/Pub Key?
|Elliptic Curve DSA | sha256, sha512    | `:es256`, `:es512` | Yes
|RSASSA PSS         | sha256, sha512    | `:ps256`, `:ps512` | Yes
|RSASSA PKCS1 v1_5  | sha256, sha512    | `:rs256`, `:rs256` | Yes
|HMAC               | sha256*, sha512   | `:hs256`, `:hs256` | No
|=====================================================================================


=== Signing data

Let start with signing data. For it we will use the `sign` function from `buddy.sign.jws` namespace:
and the `hs256` algorithm for signining:

[source, clojure]
----
(require '[buddy.sign.jws :as jws])

(jws/sign {:userid 1} "secret")
;; "eyJ0eXAiOiJKV1MiLCJhbGciOiJIU..."
----

The `sign` function return a encoded and signed token as plain `String` instance or an exception in
case of something goes wrong. As you can observe, no algorithm is passed as parameter.
In this situations the default one will be used, and in this case is `:hs256`.

NOTE: Due to the nature of the storage format, the input is restricted mainly to json objects in the current version.


=== Unsigning data

It's time to unsing data. That process consists on verify the signature of incoming data and return the
plain data (without signature). For it we will use the `unsign` function from `buddy.sign.jws`
namespace:

[source, clojure]
----
(jws/unsign data "secret")
;; => {:userid 1}
----

You do not need specify the algorithm explicitly, it is automatically detected.


=== Claims validation

_buddy-sign_ json web signature implementation also includes validation of a concrete subset
of claims related to the age of the signed data: *exp* (expiration time), *nbf* (not before).

The validation is performed on decoding the token. If `:exp` claim is found and is posterior to
the current date time (UTC) an validation exception will be raised or a failure instance will
be returned, depending on that api you are using direct or monadic.

Let see an example using direct api:

[source, clojure]
----
(require '[clj-time.core :as time])

;; Define claims with `:exp` key
(def claims
  {:user 1 :exp (time/plus (time/now) (time/seconds 5))})

;; Serialize and sign a token with previously defined claims
(def token (jws/sign claims "key"))

;; wait 5 seconds and try unsign it

(jws/unsign token "key")
;; => ExceptionInfo throw+: {:type :validation, :cause :exp, :message "Token is older than :exp (1427836475)"}
----

The best way to capture this type of exceptions is using
link:https://github.com/scgilardi/slingshot[slingshot] library:

[source, clojure]
----
(require '[slingshot.slingshot :refer [try+]])

(try+
  (jws/unsign token "key")
  (catch [:type :validation] e
    (println "Error: " (:message e))))
;; => Will print in console: "Error: Token is older than :exp (1427836475)"
----


=== Digital signature algorithms

In order to use any of digital signature algorithms you must have a private/public key.
If you don't have one, don't worry, it is very easy to generate it using *openssl*, see
this <<generate-keypairs,faq entry>>.

Now, having generated a key pair, you can sign your messages using one
of supported digital signature algorithms.

.Example of signing a string using _es256_ (eliptic curve dsa) algorithm.
[source, clojure]
----
(require '[buddy.core.keys :as keys])

;; Create keys instances
(def ec-privkey (keys/private-key "ecprivkey.pem"))
(def ec-pubkey (keys/public-key "ecpubkey.pem"))

;; Use them like plain secret password with hmac algorithms for sign
(def signed-data (jws/sign {:foo "bar"} ec-privkey {:alg :es256}))

;; And unsign
(def unsigned-data (jws/unsign signed-data ec-pubkey {:alg :es256}))
----


=== Monadic api

If you are using monadic composition with cats library you're in luck, because _buddy-sign_ also
offers monadic api. You can use it with `encode` and `decode` functions, that return the _Exception_
monad types: the `Success` instances when everything goes ok, and the `Failure` instances when
something goes wrong.

[source, clojure]
----
(jws/encode {:userid 1} "secret")
;; => #<Success [eyJ0eXAiOiJKV1MiLCJhbGciOiJIU...]>
----

The _Exception_ monad instances, for convenience implements the clojure `IDeref` interface that
makes they play well with `@something` syntax for extract value from them:

[source, clojure]
----
@(jws/encode {:userid 1} "secret")
;; "eyJ0eXAiOiJKV1MiLCJhbGciOiJIU..."
----


== Json Web Encryption

[small]#New in version: 0.5#

JSON Web Encryption (JWE) is a encryption part of Json Web Token (JWT) specification and represents
a encrypted content using JavaScript Object Notation (JSON) based data structures.


=== Supported algorithms

The Json Web Encryption in difference to JWS uses two types of algoritms: key encryption algorithms
and content encryption algorithms.

The *key encryption algorithms* are responsible of encrypt the key that will be used for encrypt the
content. This is a table that exposes the currently supported _Key Encryption Algorithms_ (specified
in JWA RFC):

.Supported Key Encryption Algorithms
[options="header", cols="1,2,1,1"]
|===================================================================================
| Algorithm name | Decription | Keyword       | Shared Key Size
| DIR            | Direct use of a shared symmetric key | `:dir` | (depends on content
encryption algorithm)
| A128KW         | AES128 Key Wrap | `:a128kw` | 16 bytes
| A192KW         | AES192 Key Wrap | `:a192kw` | 24 bytes
| A256KW         | AES256 Key Wrap | `:a256kw` | 32 bytes
| RSA1_5         | RSA PKCS1 V1_5  | `:rsa1_5` | Asymetric key pair
| RSA-OAEP       | RSA OAEP with SHA1 | `:rsa-oaep` | Asymetric key pair
| RSA-OAEP-256   | RSA OAEP with SHA256 | `:rsa-oaep-256` | Asymetric key pair
|===================================================================================


The *conentent encryption algoritms* are responsible of encrypt the content. This is a table
that exposes the currently supported _Content Encryption Algorithms_ (all specified
in the JWA RFC):

.Supported Content Encryption Algorithms
[options="header", cols="1,1,1,1"]
|===================================================================================
| Algorithm name | Description | Keyword | Shared Key Size
| A128CBC-HS256  | AES128 with CBC mode and HMAC-SHA256  | `:a128-hs256` | 32 bytes
| A192CBC-HS384  | AES192 with CBC mode and HMAC-SHA384  | `:a192-hs384` | 48 bytes
| A256CBC-HS512  | AES256 with CBC mode and HMAC-SHA512  | `:a256-hs512` | 64 bytes
| A128GCM        | AES128 with GCM mode | `:a128gcm`    | 16 bytes
| A192GCM        | AES192 with GCM mode | `:a192gcm`    | 24 bytes
| A256GCM        | AES256 with GCM mode | `:a256gcm`    | 32 bytes
|===================================================================================


=== Encrypting data

Let start with encrypting data. For it we will use the `encrypt` function from the
`buddy.sign.jwe` namespace:

[source, clojure]
----
(require '[buddy.sign.jwe :as jwe])
(require '[buddy.core.hash :as hash])

;; Hash your secret key with sha256 for
;; create a byte array of 32 bytes because
;; is a requirement for default content
;; encryption algorithm

(def secret (hash/sha256 "mysecret"))

;; Encrypt it using the previously
;; hashed key

(jwe/encrypt {:userid 1} secret {:alg :dir :enc :a128-hs256})
;; "eyJ0eXAiOiJKV1MiLCJhbGciOiJIU..."
----

The `encrypt` function, like `sign` from *JWS*, returns a plain string with encrypted and encoded
content using a provided algorithm and shared secret key.


=== Decrypting Data

The decrypt is a inverse process, that takes encrypted data and the shared key, and returns the
plain data. For it, _buddy-sign_ exposes the `decrypt` function. Let see how you can use it:

[source, clojure]
----
(jwe/decrypt incoming-data secret)
;; => {:userid 1}
----

You do not need specify the encryption algorithm explicitly, it is automatically detected, because the
incoming token will come with content encryption algorithm stored in its header part.


=== Claims validation

_buddy-sign_ json web encryption, like *jws*, also implements validation of a concrete subset
of claims related to the age of the signed data: *exp* (expiration time), *nbf* (not before).

The validation is performed on decoding the token. If `:exp` claim is found and is posterior to
the current date time (UTC) an validation exception will be raised or a failure instance will
be returned, depending on that api you are using direct or monadic.

Let see an example using direct api:

[source, clojure]
----
(require '[clj-time.core :as time])

;; Define claims with `:exp` key
(def claims
  {:user 1 :exp (time/plus (time/now) (time/seconds 5))})

;; Serialize and sign a token with previously defined claims
(def token (jwe/encrypt claims secret))

;; wait 5 seconds and try unsign it

(jwe/decrypt token secret)
;; => ExceptionInfo throw+: {:type :validation, :cause :exp, :message "Token is older than :exp (1427836475)"}
----

The best way to capture this type of exceptions is using
link:https://github.com/scgilardi/slingshot[slingshot] library:

[source, clojure]
----
(require '[slingshot.slingshot :refer [try+]])

(try+
  (jwe/encrypt token secret)
  (catch [:type :validation] e
    (println "Error: " (:message e))))
;; => Will print in console: "Error: Token is older than :exp (1427836475)"
----


== Compact message signing

Compact high level message signing implementation.

It has high influence by django's cryptographic library and json web signature/encryption
but with focus on have a compact representation. It's build on top of fantastic ptaoussanis/nippy
serialization library.

This singing implementation is not very efficient with small messages, but is very space efficient
with big messages.

The purpose of this implementation is for secure message transfer, it is not really good candidate
for auth token because of not good space efficiency for small messages.

.Supported Algorithms
[options="header"]
|=====================================================================================
| Algorithm name     | Hash algorithms   | Keywords           | Priv/Pub Key?
| Elliptic Curve DSA | sha256, sha512    | `:es256`, `:es512` | Yes
| RSASSA PSS         | sha256, sha512    | `:ps256`, `:ps512` | Yes
| RSASSA PKCS1 v1_5  | sha256, sha512    | `:rs256`, `:rs256` | Yes
| Poly1305           | aes, twofish, serpent | `:poly1305-aes`, `:poly1305-serpent`, `:poly1305-twofish` | No
| HMAC               | sha256*, sha512   | `:hs256`, `:hs256` | No
|=====================================================================================

+++*+++ indicates the default value.

[NOTE]
====
Only HMAC and Poly1305 based algorithms support plain text secret keys, If you want to use
Digital Signature instead of hmac then you must have a key pair (public and private).
====


=== Signing and unsigning data

With difference with jwt/jws, this implementation is not limited to hash-map like objects,
and you can sign any clojure valid type. Let see an example:

[source,clojure]
----
(require '[buddy.sign.compact :as cm])

;; Sign data using default `:hs256` algorithm that does not
;; requres special priv/pub key.
(def data (cm/sign {:userid 1} "secret"))

;; data will contains omething to
;; "auJ0eXAiOiJKV1MiLCJhbGciOiJIU..."

(cm/unsign data "secret")
;; => {:userid 1}
----

Then, you also will be able validate the signed message based in its age:

[source,clojure]
----
(cm/unsign data "secret" {:max-age (* 15 60)})
;; => ExceptionInfo throw+: {:type :validation, :cause :exp, :message "Token is older than 1427836475"}
----


== FAQ

[[generate-keypairs]]
=== How I can generate keypairs?

.Example on how to generate one Elliptic Curve DSA keypair.
[source, bash]
----
# Generating params file
openssl ecparam -name prime256v1 -out ecparams.pem

# Generate a private key from params file
openssl ecparam -in ecparams.pem -genkey -noout -out ecprivkey.pem

# Generate a public key from private key
openssl ec -in ecprivkey.pem -pubout -out ecpubkey.pem
----

.Example on how to generate one RSA keypair.
[source, bash]
----
# Generate aes256 encrypted private key
openssl genrsa -aes256 -out privkey.pem 2048

# Generate public key from previously created private key.
openssl rsa -pubout -in privkey.pem -out pubkey.pem
----

== How to Contribute?

=== Philosophy

Five most important rules:

- Beautiful is better than ugly.
- Explicit is better than implicit.
- Simple is better than complex.
- Complex is better than complicated.
- Readability counts.

All contributions to _buddy-sign_ should keep these important rules in mind.


=== Procedure

**buddy-sign** unlike Clojure and other Clojure contrib libs, does not have many
restrictions for contributions. Just follow the following steps depending on the
situation:

**Bugfix**:

- Fork the GitHub repo.
- Fix a bug/typo on a new branch.
- Make a pull-request to master.

**New feature**:

- Open new issue with the new feature proposal.
- If it is accepted, follow the same steps as "bugfix".


=== License

_buddy-sign_ is licensed under Apache 2.0 License. You can see the complete text
of the license on the root of the repository on `LICENSE` file.
